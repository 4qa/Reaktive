apply from: '../target.gradle'
final target = currentTarget()

kotlin {
    final taskConfigurationMap = [
            'kotlinMultiplatform': target.isMeta(),
            'metadata'           : target.isMeta(),
            'jvm'                : target.isCommon(),
            'js'                 : target.isCommon(),
            'androidDebug'       : target.isCommon(),
            'androidRelease'     : target.isCommon(),
            'linuxX64'           : target.isCommon(),
            'linuxArm32Hfp'      : target.isCommon(),
            'ios32'              : target.isIos(),
            'ios64'              : target.isIos(),
            'iosSim'             : target.isIos()
    ]
    // meta publication requires all target be included
    // should be done with android sdk installed, otherwise presents.android will crash configuration step
    // ios targets will be automatically disabled
    final configurationTargets = taskConfigurationMap.keySet().collect { targets.findByName(it) }.findAll { it != null }
    configure(configurationTargets) {
        mavenPublication { activePublication ->
            tasks.withType(AbstractPublishToMaven).all {
                onlyIf {
                    taskConfigurationMap[publication.name]
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = property('user')
    key = property('key')
    override = true // for multi-platform Kotlin/Native publishing

    pkg {
        repo = 'maven'
        name = 'reaktive'
        userOrg = 'badoo'
        vcsUrl = 'https://github.com/badoo/Reaktive.git'
        licenses = ['Apache-2.0']
        version.name = reaktive_version
    }
}

// This is required for K/N publishing
bintrayUpload.dependsOn publishToMavenLocal

bintrayUpload.doFirst {
    publications = project.publishing.publications.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it.name
    }
}